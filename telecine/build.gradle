// Manifest version information!
def versionMajor = 1
def versionMinor = 5
def versionPatch = 1
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

buildscript {
  dependencies {
    classpath 'com.android.tools.build:gradle:1.5.0'
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
  }
}

def getBugsnagKey() {
  return hasProperty('TELECINE_BUGSNAG_KEY') ? TELECINE_BUGSNAG_KEY : ''
}
def getAnalyticsKey() {
  return hasProperty('TELECINE_ANALYTICS_KEY') ? TELECINE_ANALYTICS_KEY : ''
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
  compileSdkVersion 23
  buildToolsVersion '23.0.2'

  defaultConfig {
    applicationId 'com.jakewharton.telecine'

    minSdkVersion 21
    targetSdkVersion 22

    versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    buildConfigField "String", "BUGSNAG_KEY", "\"${getBugsnagKey()}\""
    buildConfigField "String", "ANALYTICS_KEY", "\"${getAnalyticsKey()}\""
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  lintOptions {
    textReport true
    textOutput 'stdout'
    warning 'MissingTranslation'
    warning 'ResourceType' // TODO remove when updated to Butter Knife 8.0.
  }

  dexOptions {
    preDexLibraries = Boolean.valueOf(System.getProperty("pre-dex", "true"))
  }

  signingConfigs {
    debug {
      storeFile rootProject.file('debug.keystore')
      storePassword 'android'
      keyAlias 'android'
      keyPassword 'android'
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix '.debug'
      signingConfig signingConfigs.debug
    }
  }
}

dependencies {
  compile 'com.android.support:support-annotations:23.2.1'
  compile 'com.android.support:design:23.4.0'
  compile 'com.google.android.gms:play-services-analytics:8.4.0'
  compile 'com.jakewharton:butterknife:7.0.1'
  compile 'com.jakewharton.timber:timber:4.1.2'
  compile 'com.bugsnag:bugsnag-android:3.4.0'
  compile 'com.squareup.dagger:dagger:1.2.2'
  apt 'com.squareup.dagger:dagger-compiler:1.2.2'
  testCompile 'junit:junit:4.12'
  testCompile 'com.google.truth:truth:0.28'
}
