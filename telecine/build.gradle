// Manifest version information!
def versionMajor = 1
def versionMinor = 6
def versionPatch = 2
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

buildscript {
  dependencies {
    classpath 'com.android.tools.build:gradle:3.0.0-alpha3'
  }
}

def getBugsnagKey() {
  return hasProperty('TELECINE_BUGSNAG_KEY') ? TELECINE_BUGSNAG_KEY : ''
}
def getAnalyticsKey() {
  return hasProperty('TELECINE_ANALYTICS_KEY') ? TELECINE_ANALYTICS_KEY : ''
}

apply plugin: 'com.android.application'

android {
  compileSdkVersion 25
  buildToolsVersion '25.0.2'

  defaultConfig {
    applicationId 'com.jakewharton.telecine'

    minSdkVersion 21
    targetSdkVersion 22

    versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    buildConfigField "String", "BUGSNAG_KEY", "\"${getBugsnagKey()}\""
    buildConfigField "String", "ANALYTICS_KEY", "\"${getAnalyticsKey()}\""
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  lintOptions {
    textReport true
    textOutput 'stdout'

    // We run a full lint analysis as build part in CI, so skip vital checks for assemble tasks.
    checkReleaseBuilds false

    warning 'MissingTranslation'
    error 'UnusedResources'
    error 'CheckResult'
  }

  dexOptions {
    preDexLibraries = Boolean.valueOf(System.getProperty("pre-dex", "true"))
  }

  signingConfigs {
    debug {
      storeFile rootProject.file('debug.keystore')
      storePassword 'android'
      keyAlias 'android'
      keyPassword 'android'
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix '.debug'
      signingConfig signingConfigs.debug
    }
  }
}

dependencies {
  implementation 'com.android.support:support-annotations:25.1.0'
  implementation 'com.android.support:design:25.1.0'
  implementation 'com.google.android.gms:play-services-analytics:10.0.1'
  implementation 'com.nightlynexus.demomode:demomode:0.1.1'
  implementation 'com.jakewharton:butterknife:8.4.0'
  annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
  implementation 'com.jakewharton.timber:timber:4.3.1'
  implementation 'com.bugsnag:bugsnag-android:3.7.0'
  implementation 'com.google.dagger:dagger:2.8'
  annotationProcessor 'com.google.dagger:dagger-compiler:2.8'
  testImplementation 'junit:junit:4.12'
  testImplementation 'com.google.truth:truth:0.30'
}
